/*
 * Copyright (c) 2017-2018 Chris K Wensel <chris@wensel.net>. All Rights Reserved.
 *
 * This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/.
 */

buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "io.spring.gradle:spring-bintray-plugin:0.11.1"
  }
}

plugins {
  id "net.ltgt.errorprone" version "0.0.13"
}

apply from: 'etc/dependencyVersions.gradle'
apply from: 'etc/version.gradle'
apply from: 'etc/s3Upload.gradle'

allprojects {

  apply plugin: 'idea'
  apply plugin: 'eclipse'

  repositories {
    mavenLocal()
    mavenCentral()
    maven {url = 'https://repository.apache.org'}
  }
}

project.ext.distDir = null

task assemble {
  doFirst {
    println "Assembling version: ${releaseVersion}"
  }
}

task prepareDist() {
  project.distDir = file( "${rootDir}/build/dist" )
  doLast {
    mkdir( distDir )
  }
}

ext.copyright = '<i>Copyright &#169; 2017-2018 Chris K Wensel. All Rights Reserved.</i>'

subprojects {

  apply plugin: "io.spring.bintray"
  apply plugin: 'java-library'
  apply plugin: 'maven-publish'
  apply from: '../etc/testing.gradle'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  group = 'io.heretical'
  version = releaseVersion

  configurations {

    testArtifacts {
      extendsFrom testRuntime
    }
  }

  bintray {
    bintrayUser = repoUserName
    bintrayKey = repoPassword

    org = 'heretical'
    repo =  isFinalRelease ? 'release' : 'wip'

    overrideOnUpload = true

    publication = 'mavenJava'

    licenses = ['MPL-2.0']
    websiteUrl = 'http://www.heretical.io/projects/pointer-path/'
    issueTrackerUrl = 'https://github.com/Heretical/pointer-path/issues'
    vcsUrl = 'https://github.com/Heretical/pointer-path.git'
  }

  javadoc.dependsOn << prepareDist

  javadoc {
    title = "Pointer Path ${releaseVersion}"
    destinationDir = file( "${distDir}/javadoc/${project.name}" )

    failOnError = false

    configure( options ) {
      linkSource = true
      encoding = "UTF8"
      bottom = copyright
      links = [
        'http://docs.oracle.com/javase/7/docs/api/',
        'http://junit.sourceforge.net/javadoc/'
      ]
    }
  }

  task sourcesJar( type: Jar, dependsOn: classes ) {
    from sourceSets.main.allSource
    classifier = 'sources'
  }

  task javadocJar( type: Jar, dependsOn: javadoc ) {
    classifier = 'javadoc'
    from javadoc.destinationDir
  }

  task testsJar( type: Jar, dependsOn: testClasses ) {
    from sourceSets.test.output
    classifier = 'tests'
  }

  task testSourcesJar( type: Jar, dependsOn: classes ) {
    from sourceSets.test.allSource
    classifier = 'test-sources'
  }

  artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
    archives testsJar
    archives testSourcesJar
    testArtifacts testsJar
    testArtifacts testSourcesJar
  }

  dependencies {
    testCompile group: 'junit', name: 'junit', version: junitVersion
  }

  test {
    include '**/*Test.class'
    ignoreFailures = !System.getProperty( 'test.haltonerror', "true" ).equals( "true" )
  }

  assemble.dependsOn << prepareDist

  assemble.doLast {

    copy {
      into "${distDir}/lib/${archivesBaseName}"
      from( configurations.compile.resolvedConfiguration.firstLevelModuleDependencies
        .collect {dep ->
        dep.moduleArtifacts.collect {it.file}
      } )
    }

    copy {
      into distDir
      from configurations.default.allArtifacts.files
    }
  }

  publishing {
    publications {
      mavenJava( MavenPublication ) {
        artifact sourcesJar
        artifact javadocJar

        from components.java

        pom {
          name = 'Pointer-Path'
          description = 'An API for building nested data structures.'
          url = 'http://www.heretical.io/'
          inceptionYear = '2017'
          licenses {
            license {
              name = 'Mozilla Public License, v. 2.0'
              url = 'http://mozilla.org/MPL/2.0/'
              distribution = 'repo'
            }
          }
          developers {
            developer {
              id = 'cwensel'
              name = 'Chris K Wensel'
              email = 'chris@wensel.net'
            }
          }
          scm {
            url = 'https://github.com/Heretical/pointer-path.git'
          }
        }
      }
    }
  }
}

task allJavadoc( type: Javadoc, dependsOn: subprojects.javadoc ) {

  title = "Cascading ${releaseVersion}"
  destinationDir = file( "${distDir}/javadoc/all/" )

  source subprojects.javadoc.source
  classpath = rootProject.files( subprojects.javadoc.classpath )

  failOnError = false

  configure( options ) {
    linkSource = true
    encoding = "UTF8"
    bottom = copyright
    links = [
      'http://docs.oracle.com/javase/7/docs/api/',
      'http://junit.sourceforge.net/javadoc/'
    ]
  }
}

task prepareRelease( type: Copy, dependsOn: [':prepareDist', 'project:build', ':allJavadoc'] ) {

  s3UploadDocs.dependsOn << prepareRelease
  s3UploadArtifacts.dependsOn << prepareRelease

  // copy dist and apply any analytics scripts
  def analytics = System.getProperty( 'publish.docs.analytics', '' )

  from "${distDir}/javadoc/"
  into "${s3UploadDocs.source}/javadoc"

  filter {line ->
    line.replaceAll( "</body>", "${analytics}\n</body>" )
  }
}

prepareRelease.doLast {
  logger.info( 'built all artifacts' )
}

task clean {
  doLast {
    delete buildDir
  }
}
task buildDist( dependsOn: prepareRelease ) {
  doLast {
    copy {
      from 'CHANGES.txt'
      from 'README.txt'
      from 'apl.txt'
      from 'LICENSE.txt'
      into distDir
    }
  }
}

task buildPackage( type: Tar, dependsOn: buildDist ) {

  description = "package current build, does not run tests"

  destinationDir = file( s3UploadArtifacts.source )
  baseName = "${rootProject.name}-${releaseVersion}"
  compression = "GZIP"

  into( baseName ) {
    from distDir
  }
}

task buildLatestMeta( dependsOn: buildPackage ) {

  s3UploadArtifacts.dependsOn << buildLatestMeta

  ext.latestArchivePath = null
  ext.latestReleaseTagPath = null
  ext.latestPropertiesPath = null
}

buildLatestMeta.doLast {

  def destination = s3UploadArtifacts.source
  def releaseTar = buildPackage.archivePath.name
  def releaseURL = "http://${s3UploadArtifacts.destination}${releaseTar}"

  latestArchivePath = file( destination, 'latest.txt' )
  latestArchivePath.write( releaseURL )

  latestReleaseTagPath = file( destination, 'latest-tag.txt' )
  latestReleaseTagPath.write( "${releaseTag}" )

  latestPropertiesPath = file( destination, 'latest.properties' )
  latestPropertiesPath.write( "${rootProject.name}.release.version=${releaseVersion}\n" +
    "${rootProject.name}.release.major=${majorVersion}\n" +
    "${rootProject.name}.release.minor=${minorVersion}\n" +
    "${rootProject.name}.release.build=${buildNumber}\n" +
    "${rootProject.name}.release.commit=${currentCommit}\n" +
    "${rootProject.name}.release.tag=${releaseTag}\n" +
    "${rootProject.name}.release.name=${releaseTar}\n" +
    "${rootProject.name}.release.url=${releaseURL}"
  )

  latestJSONPath = file( destination, 'latest.json' )
  latestJSONPath.write( """\
{
  "${rootProject.name}": {
    "release": {
      "name": "${releaseTar}",
      "url": "${releaseURL}",
      "version": "${releaseVersion}",
      "major": "${majorVersion}",
      "minor": "${minorVersion}",
      "build": "${buildNumber}",
      "commit": "${currentCommit}",
      "tag": "${releaseTag}"
    },
    "docs": {
      "userguide": {
        "url": "http://${s3UploadDocs.destination}userguide/"
      },
      "api": {
        "url": "http://${s3UploadDocs.destination}javadoc/",
        "names": [
          "${rootProject.name}-core"
        ]
      }
    }
  }
}"""
  )
}
